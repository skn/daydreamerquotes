apply plugin: 'com.android.application'

android {
    def keystore_properties = file('/Users/srijith/android/keys/im-skn-release-keystore.properties')
    if (keystore_properties.exists()) {
        Properties props = new Properties()
        props.load(new FileInputStream(keystore_properties))
        props.each { prop ->
            project.ext.set(prop.key, prop.value)
        }
    }
    // Read passwords from Gradle properties and if not found, then from environment variables
    def keystore_file= project.hasProperty('KEYSTORE_FILE') ? KEYSTORE_FILE: System.getenv("KEYSTORE_FILE")
    def keystore_password = project.hasProperty('KEYSTORE_PASSWORD') ? KEYSTORE_PASSWORD : System.getenv("KEYSTORE_PASSWORD")
    def key_alias = project.hasProperty('KEY_ALIAS') ? KEY_ALIAS : System.getenv("KEY_ALIAS")
    def key_password = project.hasProperty('KEY_PASSWORD') ? KEY_PASSWORD : System.getenv("KEY_PASSWORD")


    //  Oct 2022: New apps required to target API level 31
    defaultConfig {
        minSdkVersion 17 // lower bound for app, determines which of a userâ€™s devices an app can be installed on
        targetSdkVersion 31 // which Android version the app was designed and tested on
        compileSdkVersion 28 // which Android SDK version will be used by gradle to compile your app
        applicationId "im.skn.daydreamerquoth"

    }
    signingConfigs {
        release {
            storeFile file(keystore_file)
            storePassword keystore_password
            keyAlias key_alias
            keyPassword key_password
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            proguardFiles 'proguard.cfg'
            zipAlignEnabled true
            applicationVariants.all { variant ->
                variant.outputs.all {
                    outputFileName = "QuotestheDaydreamer-${variant.name}-${variant.versionName}.apk"
                }
            }
        }
    }
    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
    namespace 'im.skn.daydreamerquoth'
}

dependencies {
    // Support v4
    implementation 'com.android.support:support-v4:19.0.0'
    // Robolectric
    testImplementation "org.robolectric:robolectric:4.7.3"
    testImplementation 'junit:junit:4.12'
    implementation 'androidx.preference:preference:1.1.1'
    implementation 'androidx.test:core:1.4.0'
}
